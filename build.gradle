buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
    }
}

apply plugin: "java"
apply plugin: "spring-boot"

version = "1.1.0"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

javadoc {
    options.encoding = defaultEncoding
}

test {
    include "**/model/**", "**/controller/**", "**/util/**", "**/context/**"
}

repositories {
    jcenter()
}

dependencies {
    compile "org.projectlombok:lombok:1.16.6"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework:spring-orm:4.2.4.RELEASE"
    compile "org.hibernate:hibernate-core:5.0.6.Final"
    compile "org.hibernate:hibernate-java8:5.0.6.Final"
    compile "org.hibernate:hibernate-ehcache:5.0.6.Final"
    compile "org.jboss.spec.javax.transaction:jboss-transaction-api_1.2_spec:1.0.0.Final"
    compile "net.sf.ehcache:ehcache-core:2.6.+"
    compile "com.zaxxer:HikariCP:2.4.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.6.+"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.+"
    compile "commons-io:commons-io:2.4"
    compile "commons-collections:commons-collections:3.2.+"
    compile "org.apache.commons:commons-lang3:3.3.+"
    compile "com.ibm.icu:icu4j:54.1.+"
    compile fileTree(dir: 'libs', includes: ['*.jar'])
    runtime "com.h2database:h2:1.4.+"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.10"
}

// 自分のプロジェクトとしてディープコピーしたい時は以下の変数を編集した後
// 「 gradlew copyProject 」を実行してください。
def yourProjectName = "sample"
def yourProjectPackage = "your.sample"
def yourProjectPath = "../${yourProjectName}"
task copyProject << {
    def projectPath = yourProjectPath
    def packagePath = yourProjectPackage.replaceAll(/\./, "/")
    def javaPath = "${projectPath}/src/main/java/${packagePath}"
    def testPath = "${projectPath}/src/test/java/${packagePath}"
    copy {
        from (".") { exclude ".*", "bin", "build", "LICENSE", "src/main/java", "src/test/java" }
        into projectPath
    }
    ant.copy(file: ".gitignore", todir: "${projectPath}")
    copy {
        from ("src/main/java/sample")
        into javaPath
    }
    copy {
        from ("src/test/java/sample")
        into testPath
    }
    ant.replace(token: "sample-boot-hibernate", value: yourProjectName) {
        fileset(dir: projectPath) {
            include(name: "README.md")
            include(name: "settings.gradle")
            include(name: "src/main/resources/application.yml")
        }
    }
    ant.replace(token: "packageToScan: sample", value: "packageToScan: ${yourProjectPackage}") {
        fileset(dir: projectPath) { include(name: "src/main/resources/application.yml") }
    }
    ant.replace(token: "logger name=\"sample\"", value: "logger name=\"${yourProjectPackage}\"") {
        fileset(dir: projectPath) { include(name: "src/main/resources/logback.xml") }
    }
    ant.replace(token: "import sample", value: "import ${yourProjectPackage}") {
        fileset(dir: javaPath) { include(name: "**/*.java") }
        fileset(dir: testPath) { include(name: "**/*.java") }
    }
    ant.replace(token: "package sample", value: "package ${yourProjectPackage}") {
        fileset(dir: javaPath) { include(name: "**/*.java") }
        fileset(dir: testPath) { include(name: "**/*.java") }
    }
    
}
